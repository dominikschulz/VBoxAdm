package VBoxAdm::SMTP::Server;

use strict;
use warnings;

use IO::Socket;
use File::Temp;
use Carp qw(carp croak);

our $VERSION = '@VERSION@';

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub new {
    my $this = shift;
    my @opts = @_;

    my $class = ref($this) || $this;
    my $self = bless {@opts}, $class;
    if ( !$self->{sock} ) {
        $self->{sock} = IO::Socket::INET6->new(
            LocalAddr => $self->{interface},
            LocalPort => $self->{port},
            Proto     => 'tcp',
            Type      => SOCK_STREAM,
            Listen    => 65536,
            Reuse     => 1,
        );
        $self->{sockmode} = 'accept';
    }
    else {
        $self->{sockmode} = 'direct', $self->{'s'} = $self->{sock};
    }

    if ( !defined( $self->{sock} ) ) {
        croak("$0: Socket bind failure: $!\n");
    }
    if ( $self->{sockmode} eq 'accept' ) {
        $self->{state} = 'just bound';
    }
    else {
        $self->{state} = 'accepted';
    }
    return $self;
}

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub accept {
    my $self = shift;
    my @opts = @_;

    if ( $self->{sockmode} ne 'accept' ) {
        carp( "Accept not available in this mode (" . $self->{sockmode} . ")!\n" );
        return;
    }

    %{$self} = ( %{$self}, @opts );

    ( $self->{'s'}, $self->{peeraddr} ) = $self->{sock}->accept
      or croak("$0: Socket accept failure: $!\n");
    $self->{state} = 'accepted';

    return 1;
}

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub chat {
    my $self = shift;

    my $line;
    if ( $self->{state} !~ /^data/i ) {
        return 0 unless defined( $line = $self->getline );

        # strip any kind of newlines
        $line =~ s/[\r\n]*$//;
        $self->{state} = $line;

        if ( $line =~ s/^(he|eh|lh)lo\s+//i ) {

            # trim and unify spacings
            $line =~ s/\s*$//;
            $line =~ s/\s+/ /g;
            $self->{helo} = $line;
        }
        elsif ( $line =~ m/^rset\b/i ) {
            delete $self->{to};
            delete $self->{data};
            delete $self->{recipients};
            delete $self->{xforward};
        }
        elsif ( $line =~ s/^mail\s+from:\s*//i ) {
            delete $self->{to};
            delete $self->{data};
            delete $self->{recipients};

            #$line =~ s/\s*$//;
            $line =~ s/.*</</;
            $line =~ s/>.*$/>/;
            $self->{from} = $line;
        }
        elsif ( $line =~ s/^rcpt\s+to:\s*//i ) {

            #$line =~ s/\s*$//;
            #$line =~ s/\s+/ /g;
            $line =~ s/.*</</;
            $line =~ s/>.*$/>/;
            $self->{to} = $line;
            push( @{ $self->{recipients} }, $line );
        }
        elsif ( $line =~ m/^data/i ) {
            $self->{to} = $self->{recipients};
        }
        elsif ( $line =~ m/^xforward/i ) {
            if ( $line =~ m/name=\[?(\S+)\b/i ) {
                $self->{xforward}{name} = $1;
            }
            if ( $line =~ m/addr=\[?(\S+)\b/i ) {
                $self->{xforward}{addr} = $1;
            }
            if ( $line =~ m/port=\[?(\d+)\b/i ) {
                $self->{xforward}{port} = $1;
            }
            if ( $line =~ m/helo=\[?(\S+)\b/i ) {
                $self->{xforward}{helo} = $1;
            }
            if ( $line =~ m/proto=\[?(\S+)\b/i ) {
                $self->{xforward}{proto} = $1;
            }
            if ( $line =~ m/source=\[?(\S+)\b/i ) {
                $self->{xforward}{source} = $1;
            }
        }
    }
    else {
        if ( defined( $self->{data} ) ) {
            $self->{data}->seek( 0, 0 );
            $self->{data}->truncate(0);
        }
        else {

            # tempdir should be on a ramdisk an maybe /tmp on ramdisk is
            # undesirable
            $self->{data} = File::Temp::tempfile( DIR => $self->{tempdir} );
        }
        while ( defined( my $line = $self->getline ) ) {
            if ( $line eq ".\r\n" ) {
                $self->{data}->seek( 0, 0 );

                # reset xforward as defined by the spec.
                delete $self->{xforward};

                return $self->{state} = '.';
            }

            # unescape dot on beginning of the line
            $line =~ s/^\.\./\./;
            $self->{data}->print($line)
              or croak("$0: Write error while saving data\n");
        }
        return 0;
    }
    return $self->{state};
}

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub getline {
    my $self = shift;
    local $/ = "\r\n";

    return $self->{'s'}->getline unless defined $self->{debug};

    my $line = $self->{'s'}->getline;
    $self->{debug}->print($line) if $line;
    return $line;
}

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub print {
    my $self = shift;
    my @msg  = @_;

    $self->{debug}->print(@msg) if defined $self->{debug};
    $self->{'s'}->print(@msg);
    return 1;
}

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub ok {
    my $self = shift;
    my @msg  = @_;

    @msg = ("ok.") unless @msg;

    $self->print("250 @msg\r\n")
      or croak("$0: Socket write error acknowledging $self->{state}: $!\n");
    return 1;
}

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub tempfail {
    my $self = shift;
    my @msg  = @_;

    @msg = ("temporary failure.") unless @msg;

    $self->print("450 @msg\r\n")
      or croak("$0: Socket write error acknowledging $self->{state}: $!\n");
    return 1;
}

############################################
# Usage      : ????
# Purpose    : ????
# Returns    : ????
# Parameters : ????
# Throws     : no exceptions
# Comments   : none
# See Also   : n/a
sub fail {
    my $self = shift;
    my @msg  = @_;

    @msg = ("no.") unless @msg;

    $self->print("550 @msg\r\n")
      or croak("$0: Socket write error acknowledging $self->{state}: $!\n");
    return 1;
}
1;

__END__

=head1 NAME

VBoxAdm::SMTP::Server - An SMTP Server.

=head1 VERSION

This documentation refers to VBoxAdm::SMTP::Server version @VERSION@
built on @BUILDDATE@.

=head1 SYNOPSIS

    use VBoxAdm::SMTP::Server;
		my $server = VBoxAdm::SMTP::Server->new(
			interface => $interface,
			port			=> $port,
		);

=head1 DESCRIPTION

This class implements the protocol stage necessary to implement a minimal SMTP server.

=head1 SUBROUTINES/METHODS

=head2 accept

=head2 chat

=head2 fail

=head2 getline

=head2 new

=head2 ok

=head2 print

=head2 tempfail

=head1 DIAGNOSTICS

TODO

=head1 DEPENDENCIES

IO::Socket and IO::File.

=head1 INCOMPATIBILITIES

None known.

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.
Please report problems to Dominik Schulz (dominik.schulz@gauner.org)
Patches are welcome.

=head1 AUTHOR

Dominik Schulz (dominik.schulz@gauner.org)

=head1 LICENCE AND COPYRIGHT

Copyright (c) 2010 Dominik Schulz (dominik.schulz@gauner.org). All rights reserved.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 2 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

=cut
